// Heavy Minigun
// VERY HEFTY MINIGUN chambered in 20mm HE rounds.
class GSW_HeavyMinigun : GSW_Weapon replaces BFG9000
{
	action void A_FireHeavyMinigun()
	{
		if (invoker.CheckAmmo(PrimaryFire, false, true))
		{
			A_FireProjectile("GSW_HeavyMinigunBullet", random(-2.5, 2.5), true, 7.5, -2.5, pitch: random(-2.5, 2.5));
			A_StartSound("weapons/hminigun/fire", CHAN_WEAPON);
			A_Overlay(-2, "Flash");
		}
		else
			//A_StartSound("weapons/shocksmg/empty");
			A_Saw("","", 25, "BulletPuff", SF_NORANDOM|SF_NOUSEAMMO); // Supercharge-style minigun sawing.
	}
	Default
	{
		+WEAPON.AMMO_OPTIONAL
		Weapon.SlotNumber 6;
		Weapon.AmmoType "GSW_20mmMag";
		Weapon.AmmoGive 40;
		Weapon.AmmoUse 1;

		Tag "$TAG_HEAVYMINIGUN";
		Inventory.PickupMessage "$GOTHEAVYMINIGUN";

		Weapon.UpSound "weapons/hminigun/draw";
		Inventory.PickupSound "weapons/heavyweps/pickup";

		//Decal "BulletChip"; // temp
		Obituary "$KILLHEAVYMINIGUN_MELEE";
		DamageType "Punch";
	}
	States
	{
		Spawn:
			HMGN Z -1;
			Stop;
		Select:
			HMGN A 1 {
				A_Raise(12);
				A_Overlay(-3, "Hand");
			}
			Loop;
		Deselect:
			HMGN AAAAAA 1 {
				A_Lower(12);
				A_StopSound(CHAN_BODY);
				A_StopSound(CHAN_5);
			}
			HMGN A 0 A_Overlay(-3, "HandStop");
			Loop;
		Ready:
			HMGN A 1 A_WeaponReady;
			Loop;
		Fire:
			HMGN A 3 {
				A_FireHeavyMinigun();
				A_StartSound("weapons/hminigun/windup", CHAN_BODY);
				A_StartSound("weapons/hminigun/windupback", CHAN_5);
			}
			HMGN BBBCCCDD 1;
			HMGN E 2 A_FireHeavyMinigun;
			HMGN FFGH 1;
			HMGN A 0 {
				A_StartSound("weapons/hminigun/loopback", CHAN_BODY, CHANF_LOOPING);
				A_StartSound("weapons/hminigun/loop", CHAN_5, CHANF_LOOPING);
			}
			HMGN A 0 A_ReFire("Hold");
			Goto Release;
		Hold:
			HMGN A 1 A_FireHeavyMinigun;
			HMGN C 1;
			HMGN E 1 A_FireHeavyMinigun;
			HMGN G 1;
			HMGN A 0 A_ReFire("Hold");
			Goto Release;
		/*Hold2:
			HMGN AE 1 A_FireHeavyMinigun;
			HMGN A 0 A_ReFire("Hold2");
			Goto Release;*/
		Release:
			HMGN A 0 {
				A_StartSound("weapons/hminigun/winddn", CHAN_BODY);
				A_StartSound("weapons/hminigun/winddnback", CHAN_5);
			}
			HMGN AABBCCCDDDEEEFFFFGGGGHHHH 1;
			Goto Ready;
		Flash:
			HMGF A 1 Bright A_AttachLightDef("Flash", "GSW_GunFlash");
			HMGF B 1 Bright;
			HMGF B 0 A_RemoveLight("Flash");
			Stop;
		Hand:
			HMGN Y 1;
			Loop;
		HandStop:
			Stop;
	}
}

class GSW_HeavyMinigunBullet : FastProjectile
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 70;
		DamageFunction (64);
		ExplosionDamage 64;
		ExplosionRadius 64;
		Translation "0:127=[67,0,0]:[255,0,0]", "127:255=[255,0,0]:[255,223,0]";
		Obituary "$KILLHEAVYMINIGUN";
		Decal "BulletChip"; // temp
		DamageType "Bullet";
		DontHurtShooter; // I swear to god if this works // OH MY FUCKING GOD WHY IS THIS EVEN DEPRECATED
	}
	States
	{
		Spawn:
			GPRJ A -1 Bright;
			Stop;
		Death:
			TNT1 A 0 A_Explode(); // i'm so mad. passing 0 to the flags did nothing but that fucking property up there works fine
			GPRJ BCDEF 2 Bright;
			Stop;
	}
}