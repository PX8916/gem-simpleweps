// Axe
// It's an axe. Chop chop.
// Just a copy of the Pickaxe from ZandroMod...
class GSW_Axe : GSW_Weapon replaces Fist
{
	action bool IsPressed(int which) // "which" being any BT_* value
	{
		return player.cmd.buttons & which;
	}
	action state A_AxeReHold()
	{
		if (IsPressed(BT_ALTATTACK)) // if held
			return ResolveState("LongHold");
		else
			return ResolveState(null);
	}
	action state A_AxeBerserkReHold()
	{
		if (IsPressed(BT_ALTATTACK)) // if held
			return ResolveState("LongHold.Berserk");
		else
			return ResolveState(null);
	}
	Default
	{
		+WEAPON.MELEEWEAPON
		+INVENTORY.UNDROPPABLE
		+WEAPON.AXEBLOOD
		+WEAPON.NOALERT
		Weapon.SlotNumber 1;
		Inventory.Icon "AXEWZ0";

		Tag "$TAG_AXE";
		Obituary "$KILLAXE"; // Maybe add the unused "cut in half" death to the player or some monsters if I make a replacement mod to go with this.
		// No pickup message.

		Weapon.UpSound "weapons/axe/draw";

		Decal "BulletChip";
	}
	States
	{
		Ready:
			AXEW A 1 A_WeaponReady;
			Loop;
		Deselect:
			AXEW A 1 A_Lower(18);
			Loop;
		Select:
			AXEW A 1 A_Raise(18);
			Loop;
		AltFire:
		Fire:
			TNT1 A 0 A_JumpIfInventory("GSW_PowerStrength", 1, "Fire.Berserk");
			AXEW BBBCCDE 1;
			TNT1 A 8;
		AltHold:
		Hold:
			TNT1 A 0 A_JumpIfInventory("GSW_PowerStrength", 1, "Hold.Berserk");
			AXEW FGH 3;
			AXEW IJKK 2;
			AXEW K 1;
		LongHold:
			TNT1 A 0 A_JumpIfInventory("GSW_PowerStrength", 1, "LongHold.Berserk");
			AXEW K 1;
			TNT1 A 0 A_AxeReHold();
			AXEW LMN 1;
			TNT1 A 0 A_PlaySound("weapons/axe/swing");
			AXEW O 2 {
				A_CustomPunch(60, true, CPF_NORANDOMPUFFZ, "GSW_AxePuff", 96); // big fuck-off axe, so double range
				A_AlertMonsters();
			}
			TNT1 A 15;
			TNT1 A 0 A_ReFire;
			TNT1 A 3;
			AXEW EDCB 2;
			Goto Ready;
		Fire.Berserk:
			AXEW BBCDE 1;
			TNT1 A 6;
		Hold.Berserk:
			AXEW FGH 2;
			AXEW IJKK 1;
		LongHold.Berserk:
			AXEW K 1;
			TNT1 A 0 A_AxeBerserkReHold();
			AXEW LMN 1;
			TNT1 A 0 A_PlaySound("weapons/axe/swing");
			AXEW O 2 {
				A_CustomPunch(600, true, CPF_NORANDOMPUFFZ, "GSW_AxePuff", 96); // big fuck-off axe, so double range
				A_AlertMonsters();
			}
			TNT1 A 13;
			TNT1 A 0 A_ReFire("Hold.Berserk");
			TNT1 A 3;
			AXEW EDCB 2;
			Goto Ready;
	}
}
class GSW_AxePuff : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		-ALLOWPARTICLES
		-RANDOMIZE
		+FORCEPAIN
		+PUFFONACTORS
		+EXTREMEDEATH
		RenderStyle "Translucent";
		Alpha 0.5;
		VSpeed 1;
		Mass 5;
		DamageType "Saw";
	}
	States
	{
		Melee:
			PUFF A 4 Bright A_PlaySound("weapons/axe/hit", CHAN_6);
			PUFF BCD 4;
			Stop;
		XDeath:
			TNT1 A 0 A_PlaySound("weapons/axe/bleed", CHAN_6);
			Stop;
	}
}